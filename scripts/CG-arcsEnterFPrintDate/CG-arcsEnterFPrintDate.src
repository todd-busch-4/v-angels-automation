'###############################################################################################################################################
'## developer:				todd busch
'##
'## description:            processes change events in employment inquiry list. for rows with triggering stage/column status values it performs
'##							the appropriate appropriate action to move applicant out of the 'hiring decision' stage.
'##
'## run in:					firefox
'##
'## notes:                  none
'##							
'###############################################################################################################################################

'###############################################################################################################################################
'##   													script prep																			  
'###############################################################################################################################################
include "C:\v-angels-automation\framework\CG-loadWorkflowData\CG-loadWorkflowData.src"
include "C:\v-angels-automation\framework\CG-globalFunctions\CG-globalFunctions.src"
include "C:\v-angels-automation\framework\CG-arcsFunctions\CG-arcsFunctions.src"
include "c:\v-angels-automation\framework\CG-globalWinFunctions\CG-globalWinFunctions.src"
include "c:\v-angels-automation\framework\CG-globalWebFunctions\CG-globalWebFunctions.src"
include "c:\v-angels-automation\framework\CG-globalWebValidations\CG-globalWebValidations.src"
include "C:\v-angels-automation\framework\CG-globalLogging\CG-globalLogging.src"
include "C:\v-angels-automation\framework\CG-globalSubDefinitions\CG-globalSubDefinitions.src"
include "C:\v-angels-automation\framework\CG-globalVariables\CG-globalVariables.src"
include "C:\v-angels-automation\object-repos\arcs\arcsObjectRepository.src"


currScript$ = "CG-arcsEnterFPrintDate"


#IgnoreErrors = 1


'------------------------------------------------------------------------------------------------------------------------------  read in columns from employment inquiry doc
if LoadWorkflowData(currScript$) = 1 then
	criticalStop()
endif


'###############################################################################################################################################
'##   													script start																		  
'###############################################################################################################################################
logStartScript("enter fingerprint date")

'------------------------------------------------------------------------------------------------------------------------------ begin iterating through change event column

i = 0


repeat


	'-------------------- get change status of current row
	curr_status$ = arrChangeEvents$(i)


	if curr_status$ = "Yes" then
		iEventCnt = iEventCnt + 1
		iExcelRow = i + 2
		logEventFound()
    else
        goto nextIteration
    endif

'------------------------------------------------------------------------------------------------------------------------------ stage check

	'-------------------- get stage for current change event
	curr_stage$ = arrCurrentStage$(i)


	if arcsEnterFPrintStageChk() = 0 then
		iStageCnt = iStageCnt + 1
	else
		iBypassedCnt = iBypassedCnt + 1
		goto nextIteration
	endif


	'-------------------- get data values for current record
	arcsEnterFPrintGetData()


	'-------------------- verify ssn is formatted properly
	applicantSSN$ = formatSSN$(applicantSSN$)

'------------------------------------------------------------------------------------------------------------------------------ status check

	'-------------------- check status for trigger value
	if arcsEnterFPrintStatusChk() <> 0 then
		iBypassedCnt = iBypassedCnt + 1
		goto nextIteration
	else
		iTriggeredCnt = iTriggeredCnt + 1
	endif

'------------------------------------------------------------------------------------------------------------------------------  log in

	'-------------------- invoke browser
	if webInvokeBrowser("FF", arcsURL$) = 1 then
		nextScript()
		goto nextIteration
	endif


	'-------------------- enter username
	usepage(pgArcsLogin$)
	if webEnterText(inputArcsUsername$, arcsUsername$, "entering username") <> 0 then
		goto nextIteration
	endif
	if webValidateText(inputArcsUsername$, arcsUsername$, "entering username") <> 0 then
		goto nextIteration
	endif

			
	'-------------------- enter password
	if webEnterText(inputArcsPassword$, arcsPassword$, "entering password") <> 0 then
		goto nextIteration
	endif
	if webValidateText(inputArcsPassword$, arcsPassword$, "entering password") <> 0 then
		goto nextIteration
	endif

			
	'-------------------- click login button
	if webClickElement(btnArcsLogin$, "clicking login") <> 0 then
		goto nextIteration
	endif
	if webValidateElement(imgARCSLogo$, "arcs logo", "clicking login") <> 1 then
		goto nextIteration
	endif
	pause 3

	'-------------------- accept end user agreement if presented
	if arcsSubAppAcceptUsrAgree() <> 0 then
		nextScript()
		goto nextIteration
	endif

'------------------------------------------------------------------------------------------------------------------------------ applicant search

	'-------------------- navigate to person search page
	if webNavigate(urlArcsPersonSrc$, "nav to person search") <> 0 then
		goto nextIteration
	endif
	pause 2
	if webValidateCurrPage(pgArcsPersonSrc$, "nav to person search") <> 0 then
		goto nextIteration
	endif


	'-------------------- enter ssn
	usepage(pgArcsPersonSrc$)
	if webPasteText(inputArcsSSN$, applicantSSN$, "entering ssn") <> 0 then
		goto nextIteration
	endif
	pause 2
	if webValidateText(inputArcsSSN$, applicantSSN$, "entering ssn") <> 0 then
		goto nextIteration
	endif


	'-------------------- enter last name
	if webPasteText(inputArcsLName$, applicantLName$, "entering last name") <> 0 then
		goto nextIteration
	endif
	pause 2
	if webValidateText(inputArcsLName$, applicantLName$, "entering last name") <> 0 then
		goto nextIteration
	endif


	'-------------------- click search button
	if webClickElement(btnArcsSearch$, "clicking search button") <> 0 then
		goto nextIteration
	endif
	pause 2
	if webValidateCurrPage(pgArcsPersSummary$, "clicking search button") <> 0 then
		goto nextIteration
	endif

'------------------------------------------------------------------------------------------------------------------------------ enter fingerprint date

	'--------------------click the applications tab
	if webClickElement(tabArcsPersSummApps$, "clicking applications tab") <> 0 then
		goto nextIteration
	endif
	pause 2
	if webValidateElement(lnkArcsFinalResult$, "final results link", "clicking applications tab") <> 1 then
		goto nextIteration
	endif


	'-------------------- click link to open application
	if webClickElement(lnkArcsSubmittedApp$, "clicking app status link") <> 0 then
		goto nextIteration
	endif
	pause 2
	if webValidateElement(btnArcsCloseNoHire$, "close button", "clicking app status link") <> 1 then
		goto nextIteration
	endif


	'-------------------- click enter fingerprint info button
	if webClickElement(btnArcsEnterFingerPrint$, "clicking enter fingerprint info button") <> 0 then
		goto nextIteration
	endif
	pause 2
	if webValidateElement(inputArcsFPrintDate$, "fingerprint date input field", "clicking enter fingerprint info button") <> 1 then
		goto nextIteration
	endif


	'-------------------- get date to use
	fPrintDate$ = arcsGetFPrintDate$()
	if fPrintDate$ = "1" then
		closebrowser()
		goto nextIteration
	endif
	

	'-------------------- enter date
	if webPasteText(inputArcsFPrintDate$, fPrintDate$, "entering fingerprint date") <> 0 then
		goto nextIteration
	endif
	pause 2


	'-------------------- click save
	if webClickElement(btnArcsSave$, "clicking save button") <> 0 then
		goto nextIteration
	endif
	pause 2
	if webValidateElement(btnArcsHireProv$, "hire provisionally button", "clicking save button") <> 1 then
		goto nextIteration
	endif


	'-------------------- log message
	closebrowser()
	logWorkflowSuccess("workflow completed; fingerprint date entered successfully")
	iCompletedCnt = iCompletedCnt + 1


nextIteration:


i = i + 1
	

until i = 5000	'iterate through all rows in array

'###############################################################################################################################################
'##   													script end
'###############################################################################################################################################

arcsEnterFPrintLogStats()
